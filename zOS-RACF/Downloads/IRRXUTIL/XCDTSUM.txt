/* REXX */                                                                      
/*********************************************************************/         
/*                                                                   */         
/* Copyright 2022 IBM Corp.                                          */         
/*                                                                   */         
/* Licensed under the Apache License, Version 2.0 (the "License");   */         
/* you may not use this file except in compliance with the License.  */         
/* You may obtain a copy of the License at                           */         
/*                                                                   */         
/* http://www.apache.org/licenses/LICENSE-2.0                        */         
/*                                                                   */         
/* Unless required by applicable law or agreed to in writing,        */         
/* software distributed under the License is distributed on an       */         
/* "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND,      */         
/* either express or implied. See the License for the specific       */         
/* language governing permissions and limitations under the License. */         
/*                                                                   */         
/*********************************************************************          
                                                                                
 Name:    XCDTSUM                                                               
                                                                                
 Author:  Bruce Wells - brwells@us.ibm.com                                      
                                                                                
 Purpose: Display summary of RACF classes and list by ascending                 
          POSIT number.  Can be used to confirm a POSIT number is               
          available for use.                                                    
                                                                                
 Input:   None, but a RACF class name can be specified for some                 
          reason.  If so, the summary will start with that class                
          and only include classes following it alphabetically.                 
                                                                                
 Syntax:  XCDTSUM [class-name]                                                  
                                                                                
**********************************************************************/         
parse upper arg name                                                            
                                                                                
If name = '' Then name = " "   /* Start at beginning by default */              
                                                                                
/*-------------------------------------------------------------------*/         
/* Create an ISPF table to store the user IDs contained within the   */         
/* connection list.                                                  */         
/*-------------------------------------------------------------------*/         
TBCMD="TBCREATE "      ||,   /* Create command                       */         
      "POSITS "        ||,   /* Table name                           */         
      "KEYS(POSITNUM) "||,   /* Key (db key) is posit number         */         
      "NAMES(CLASSES) "||,   /* Class names sharing posit            */         
      "NOWRITE "||,          /* temporary in-storage only table      */         
      "REPLACE "             /* Replace if already exists            */         
ADDRESS ISPEXEC TBCMD                                                           
numrows = 0                  /* Init number of rows in table         */         
                                                                                
/*-------------------------------------------------------------------*/         
/* Set up sorting for the table by posit number                      */         
/*-------------------------------------------------------------------*/         
TBCMD="TBSORT POSITS FIELDS(POSITNUM,N,A)"  /* Numeric Ascending */             
ADDRESS ISPEXEC TBCMD                                                           
                                                                                
num_tot_classes = 0          /* Total number of classes defined      */         
num_ibm_classes = 0          /* Number of IBM classes defined        */         
num_dyn_classes = 0          /* Number of dynamic classes defined    */         
num_sta_classes = 0          /* Number of customer static classes    */         
num_dup_classes = 0          /* Number of customer classes that are             
                                defined in both the static and                  
                                dynamic CDT                          */         
                                                                                
Do While myrc /= '13 13 4 4 4' /* While still classes left      */              
  MYRC=IRRXUTIL("EXTRACTN","_CDT",name,"CLS","","FALSE")                        
                                                                                
  If myrc = '0 0 0 0 0' Then Do                                                 
    POSITNUM = CLS.POSIT                                                        
                                                                                
    num_tot_classes = num_tot_classes + 1                                       
                                                                                
    If CLS.IBM_CLASS Then                                                       
      num_ibm_classes = num_ibm_classes + 1                                     
    Else Do                                                                     
      If CLS.DYNAMIC Then                                                       
        num_dyn_classes = num_dyn_classes + 1                                   
      Else                                                                      
        num_sta_classes = num_sta_classes + 1                                   
      If CLS.DUPLICATE Then                                                     
        num_dup_classes = num_dup_classes + 1                                   
    End                                                                         
                                                                                
    TBCMD="TBGET POSITS"                                                        
    ADDRESS ISPEXEC TBCMD                                                       
    If rc = 0 Then Do                                                           
      CLASSES = CLASSES CLS.CLASSNAME   /* Append next sharing class */         
   /* Say "row exists for posit number" POSITNUM "with class" CLASSES,          
          "while processing" CLS.CLASSNAME */                                   
      TBCMD="TBMOD POSITS ORDER"                                                
      ADDRESS ISPEXEC TBCMD                                                     
    End                                                                         
    Else Do                                                                     
      /*-------------------------------------------------------------*/         
      /* Add a row for the posit number returned.                    */         
      /*-------------------------------------------------------------*/         
      CLASSES = CLS.CLASSNAME                                                   
      TBCMD="TBADD POSITS ORDER"                                                
      ADDRESS ISPEXEC TBCMD                                                     
      If rc = 0 Then                                                            
        numrows = numrows + 1                                                   
    End                                                                         
  End                                                                           
  name = CLS.CLASSNAME         /* Iterate to next class         */              
End                            /* Done retrieving classes       */              
                                                                                
say "There are" num_tot_classes "classes defined, using" numrows,               
    "posit values."                                                             
say " " num_ibm_classes "are IBM classes"                                       
If num_tot_classes > num_ibm_classes Then Do                                    
  say " " num_tot_classes-num_ibm_classes "are customer-defined"                
  say "   " num_dyn_classes "of these are dynamic"                              
  say "   " num_sta_classes "of these are static"                               
  say "   " num_dup_classes "of these are both static and dynamic"              
End                                                                             
say ""                                                                          
                                                                                
/*-------------------------------------------------------------------*/         
/* Now display the information from our sorted posit table.      */             
/*-------------------------------------------------------------------*/         
say "The following posit numbers are in use:"                                   
say                                                                             
say " Posit   Class name(s)       "                                             
say " =====   ===================="                                             
address ISPEXEC "TBTOP POSITS"                                                  
do i = 1 to numrows                                                             
  TBCMD = "TBSKIP POSITS"                                                       
  ADDRESS ISPEXEC TBCMD                                                         
  if rc = 0 then                                                                
    say " "Left(POSITNUM,5) " " CLASSES                                         
end                                                                             
                                                                                
address ISPEXEC "TBEND POSITS"          /* Delete the table          */         
                                                                                
