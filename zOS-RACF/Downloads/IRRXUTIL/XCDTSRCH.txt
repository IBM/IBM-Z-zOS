/* REXX */                                                                      
/*********************************************************************/         
/*                                                                   */         
/* Copyright 2022 IBM Corp.                                          */         
/*                                                                   */         
/* Licensed under the Apache License, Version 2.0 (the "License");   */         
/* you may not use this file except in compliance with the License.  */         
/* You may obtain a copy of the License at                           */         
/*                                                                   */         
/* http://www.apache.org/licenses/LICENSE-2.0                        */         
/*                                                                   */         
/* Unless required by applicable law or agreed to in writing,        */         
/* software distributed under the License is distributed on an       */         
/* "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND,      */         
/* either express or implied. See the License for the specific       */         
/* language governing permissions and limitations under the License. */         
/*                                                                   */         
/*********************************************************************          
                                                                                
 Name:    XCDTSRCH                                                              
                                                                                
 Author:  Bruce Wells - brwells@us.ibm.com                                      
                                                                                
 Purpose: Search for RACF classes with the specified attributes.                
                                                                                
 Input:   A space-delimited set of variable=value pairs                         
                                                                                
          Variable names are documented in the IRRXUTIL chapter of              
          Macros and Interfaces.  However, the easiest thing to do              
          is use the XCDT exec to display any old class. It displays            
          the IRRXUTIL variable names for each attribute.                       
                                                                                
 Authorization required:                                                        
                                                                                
   - If any variable name representing an active SETROPTS setting is            
     specified (see setrVars below), this requires READ authority to            
     IRR.RADMIN.SETROPTS.LIST. For example:                                     
                                                                                
     RDEFINE FACILITY IRR.RADMIN.SETROPTS.LIST UACC(READ)                       
     SETROPTS RACLIST(FACILITY) REFRESH'                                        
                                                                                
 Examples:                                                                      
                                                                                
   1) Find all classes that support mixed case profile names:                   
                                                                                
      XCDTSRCH MIXED_CASE=1                                                     
                                                                                
   2) Find all classes that support OPERATIONS:                                 
                                                                                
      XCDTSRCH OPERATIONS=1                                                     
                                                                                
   3) Find all RACLIST-required classes that issue ENF signals:                 
                                                                                
      XCDTSRCH RACLIST_REQUIRED=1 ENF_SIGNAL=1                                  
                                                                                
   4) Find all RACLISTed classes without sifting through SETROPTS               
      output:                                                                   
                                                                                
      XCDTSRCH RACLIST_ACTIVE=1                                                 
                                                                                
      Note that, unlike SETROPTS LIST, this will display grouping               
      classes, even the silly meaningless ones like SECLMBR.                    
**********************************************************************/         
Parse Upper Arg options                                                         
name = " "                          /* Start at beginning by default */         
                                                                                
setrVars = "CLASS_ACTIVE RACLIST_ACTIVE GENLIST_ACTIVE GENERIC_ACTIVE",         
           "GENCMD_ACTIVE GLOBAL_ACTIVE STATISTICS_ACTIVE LOGOPTIONS",          
           "AUDIT_ACTIVE"                                                       
                                                                                
crit.0 = 0                                                                      
matchingClasses = ''                                                            
                                                                                
Do i = 1 to Words(options)                                                      
  crit.0 = crit.0 + 1                                                           
  opt    = Word(options,i)                                                      
  eq     = Pos('=',opt)                                                         
  crit.i = Substr(opt,1,eq-1)                                                   
  val.i  = Substr(opt,eq+1,Length(opt))                                         
                                                                                
  /* If searching on a SETROPTS setting, request the settings from   */         
  /* IRRXUTIL.                                                       */         
  If Wordpos(crit.i,setrVars) > 0 Then                                          
    IRRXUTIL_OPTION_CLASS_SETTINGS = 1                                          
End                                                                             
                                                                                
Do While myrc /= '13 13 4 4 4'      /* While still classes left      */         
  myrc=IRRXUTIL("EXTRACTN","_CDT",name,"CLS","","FALSE")                        
                                                                                
  If myrc = '0 0 0 0 0' Then Do                                                 
    Do j = 1 to crit.0 Until(Value("CLS."crit.j) <> val.j)                      
    End                                                                         
                                                                                
    If j > crit.0 Then                                                          
      matchingClasses = matchingClasses CLS.CLASSNAME                           
  End                                                                           
                                                                                
  Else If myrc = '12 12 8 8 24' Then Do                                         
    say "You are using a SETROPTS setting in your search criteria, but",        
        "you lack READ access to IRR.RADMIN.SETROPTS.LIST in the",              
        "FACILITY class."                                                       
    exit                                                                        
  End                                                                           
                                                                                
  Else If myrc /= '13 13 4 4 4' Then Do                                         
    say "IRRXUTIL error" myrc "after processing" name                           
    exit                                                                        
  End                                                                           
                                                                                
  name = CLS.CLASSNAME              /* Iterate to next class         */         
End                                 /* Done retrieving classes       */         
                                                                                
If matchingClasses = '' Then                                                    
  say "No classes matched your search criteria"                                 
Else Do                                                                         
  say "The following classes matched your search criteria:"                     
  Do k = 1 to Words(matchingClasses)                                            
    say " " Word(matchingClasses,k)                                             
  End                                                                           
End                                                                             
